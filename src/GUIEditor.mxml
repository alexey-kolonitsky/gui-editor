<?xml version="1.0"?>
<s:WindowedApplication
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    width="1024" height="800"
    backgroundAlpha="0.0"
    addedToStage="{addedToStageHandler(event)}"
    creationComplete="{creationCompleteHandler(event)}">

    <fx:Script>
        <![CDATA[
            import feathers.system.DeviceCapabilities;
            import feathers.themes.MetalWorksMobileTheme;

            import mx.collections.ArrayCollection;
            import mx.events.FlexEvent;

            import org.kolonitsky.alexey.StoredFieldManager;
            import org.okapp.guieditor.view.AnimationScreen;
            import org.okapp.guieditor.view.LayoutScreen;
            import org.okapp.guieditor.view.BaseScreen;

            import spark.components.Group;
            import spark.events.IndexChangeEvent;

            import starling.core.Starling;
            import starling.display.Sprite;
            import starling.events.Event;

            public static const SCREEN_LAYOUT:String = "layout";
            public static const SCREEN_ANIMATION:String = "animation";

            [Bindable]
            private var so:StoredFieldManager = StoredFieldManager.instance;

            [Bindable]
            private var starlingStage:Starling;

            [Bindable]
            private var tabCollectin:ArrayCollection = new ArrayCollection([SCREEN_LAYOUT, SCREEN_ANIMATION]);

            override protected function initializationComplete ():void
            {
                DeviceCapabilities.dpi = 326;
                StoredFieldManager.instance.initialize(Constants.APPLICATION_NAME);

                Starling.multitouchEnabled = true;

                super.initializationComplete();
            }

            private function addedToStageHandler (event:flash.events.Event):void
            {
                starlingStage = new Starling(starling.display.Sprite, stage);
                starlingStage.antiAliasing = 1;
                starlingStage.start();
                starlingStage.addEventListener(starling.events.Event.ROOT_CREATED, starling_rootCreatedHandler);
            }

            private function starling_rootCreatedHandler (event:starling.events.Event):void
            {
                new MetalWorksMobileTheme();
            }

            private var screens:Dictionary = new Dictionary();
            private var currentScreen:Group;

            private function tabs_changeHandler (event:IndexChangeEvent):void
            {
                removeElement(currentScreen);

                var key:String = listTabs.selectedItem as String;
                if (! (key in screens) || screens[key] == null)
                    screens[key] = createScreen(key);

                currentScreen = screens[key];
                addElement(currentScreen);
            }

            private function createScreen (key:String):BaseScreen
            {
                var result:BaseScreen = null;

                switch (key)
                {
                case SCREEN_ANIMATION:
                    result = new AnimationScreen();
                    break;

                case SCREEN_LAYOUT:
                    result = new LayoutScreen();
                    break;

                default:
                    trace("WARNING: createScreen. Unexpected screen id: " + key);
                    break;
                }

                result.left = 48;
                result.top = 0;
                result.bottom = 0;
                result.right = 0;
                result.starling = starlingStage;
                return result;
            }

            private function creationCompleteHandler (event:FlexEvent):void
            {
                var key:String = listTabs.selectedItem;
                screens[key] = createScreen(key);

                currentScreen = screens[key];
                addElement(currentScreen);
            }
        ]]>
    </fx:Script>

    <s:List id="listTabs"
        left="4" top="4" bottom="4" width="40"
        dataProvider="{tabCollectin}"
        selectedIndex="0"
        change="{tabs_changeHandler(event)}"/>

</s:WindowedApplication>
