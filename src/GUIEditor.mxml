<?xml version="1.0"?>
<s:WindowedApplication
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    width="1024" height="800"
    backgroundAlpha="0.0"
    addedToStage="{creationCompleteHandler(event)}">

    <fx:Script>
        <![CDATA[
            import feathers.system.DeviceCapabilities;
            import feathers.themes.MetalWorksMobileTheme;

            import flash.events.Event;

            import mx.collections.ArrayCollection;
            import mx.events.FlexEvent;

            import org.kolonitsky.alexey.StoredFieldManager;
            import org.okapp.guieditor.view.AnimationScreen;
            import org.okapp.guieditor.view.LayoutScreen;
            import org.okapp.guieditor.view.BaseScreen;

            import spark.components.Group;
            import spark.events.IndexChangeEvent;

            import starling.core.Starling;
            import starling.display.Sprite;
            import starling.events.Event;

            public static const SCREEN_LAYOUT:String = "layout";
            public static const SCREEN_ANIMATION:String = "animation";

            [Bindable]
            private var so:StoredFieldManager = StoredFieldManager.instance;

            [Bindable]
            private var starlingStage:Starling;

            [Bindable]
            private var tabCollection:ArrayCollection = new ArrayCollection([SCREEN_LAYOUT, SCREEN_ANIMATION]);

            private var screens:Dictionary = new Dictionary();
            private var currentScreen:BaseScreen;

            private function starling_rootCreatedHandler(event:starling.events.Event):void
            {
                new MetalWorksMobileTheme();

                if (currentScreen)
                    currentScreen.starling = starlingStage;
            }

            private function tabs_changeHandler(event:IndexChangeEvent):void
            {
                removeElement(currentScreen);

                var key:String = listTabs.selectedItem as String;
                if (! (key in screens) || screens[key] == null)
                    screens[key] = createScreen(key);

                StoredFieldManager.instance.setString(Constants.SO_SELECTED_SCREEN, key);

                currentScreen = screens[key];
                addElement(currentScreen);
            }

            private function createScreen(key:String):BaseScreen
            {
                var result:BaseScreen = null;

                switch (key)
                {
                case SCREEN_ANIMATION:
                    result = new AnimationScreen();
                    break;

                case SCREEN_LAYOUT:
                    result = new LayoutScreen();
                    break;

                default:
                    trace("WARNING: createScreen. Unexpected screen id: " + key);
                    break;
                }

                result.left = 48;
                result.top = 0;
                result.bottom = 0;
                result.right = 0;
                result.starling = starlingStage;
                return result;
            }

            private function creationCompleteHandler(event:flash.events.Event):void
            {
                var appFullName:String = Constants.APPLICATION_NAME + "-" + Constants.APPLICATION_VERSION;
                var sfm:StoredFieldManager = StoredFieldManager.instance;

                DeviceCapabilities.dpi = 326;

                // Initialize local storage
                sfm.initialize(appFullName);

                // Initialize starling for specialized previews
                Starling.multitouchEnabled = true;

                starlingStage = new Starling(starling.display.Sprite, stage);
                starlingStage.antiAliasing = 1;
                starlingStage.start();
                starlingStage.addEventListener(starling.events.Event.ROOT_CREATED, starling_rootCreatedHandler);

                super.initialize();

                var key:String = sfm.getString(Constants.SO_SELECTED_SCREEN);
                if (key != null)
                    listTabs.selectedItem = key;
                else
                    key = listTabs.selectedItem;

                currentScreen = screens[key] = createScreen(key);
                addElement(currentScreen);
            }
        ]]>
    </fx:Script>

    <s:List id="listTabs"
        left="4" top="4" bottom="4" width="40"
        dataProvider="{tabCollection}"
        selectedIndex="0"
        change="{tabs_changeHandler(event)}"/>

</s:WindowedApplication>
