<?xml version="1.0"?>
<!--
  Created by Alexey on 12/3/2014.
-->
<s:WindowedApplication
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    width="1024" height="800"
    backgroundAlpha="0.0"
    addedToStage="{creationCompleteHandler(event)}">

    <fx:Script>
        <![CDATA[
            import feathers.system.DeviceCapabilities;
            import feathers.themes.MetalWorksMobileTheme;

            import mx.collections.ArrayCollection;

            import org.kolonitsky.alexey.StoredFieldManager;
            import org.okapp.guieditor.view.AnimationScreen;
            import org.okapp.guieditor.view.BaseScreen;
            import org.okapp.guieditor.view.LayoutScreen;

            import starling.core.Starling;
            import starling.display.Sprite;
            import starling.events.Event;

            public static const SCREEN_LAYOUT:String = "layout";
            public static const SCREEN_ANIMATION:String = "animation";

            [Bindable]
            private var so:StoredFieldManager = StoredFieldManager.instance;

            [Bindable]
            private var starlingStage:Starling = null;

            [Bindable]
            private var tabCollection:ArrayCollection = new ArrayCollection([SCREEN_LAYOUT, SCREEN_ANIMATION]);

            private var screens:Dictionary = new Dictionary();
            private var currentScreen:BaseScreen;

            private function starling_rootCreatedHandler(event:starling.events.Event):void
            {
                new MetalWorksMobileTheme();

                if (currentScreen)
                    currentScreen.starling = starlingStage;
            }

            private function createScreen(key:String):BaseScreen
            {
                var result:BaseScreen = null;

                switch (key)
                {
                case SCREEN_ANIMATION:
                    result = new AnimationScreen();
                    break;

                case SCREEN_LAYOUT:
                    result = new LayoutScreen();
                    break;

                default:
                    trace("WARNING: createScreen. Unexpected screen id: " + key);
                    break;
                }

                result.left = 0;
                result.top = 0;
                result.bottom = 0;
                result.right = 0;

                if (starlingStage && starlingStage.context)
                    result.starling = starlingStage;
                return result;
            }

            private function creationCompleteHandler(event:flash.events.Event):void
            {
                var appFullName:String = Constants.APPLICATION_NAME + "-" + Constants.APPLICATION_VERSION;
                var sfm:StoredFieldManager = StoredFieldManager.instance;

                DeviceCapabilities.dpi = 326;

                // Initialize local storage
                sfm.initialize(appFullName);

                // Initialize starling for specialized previews
                Starling.multitouchEnabled = true;

                starlingStage = new Starling(starling.display.Sprite, stage);
                starlingStage.antiAliasing = 1;
                starlingStage.start();
                starlingStage.addEventListener(starling.events.Event.ROOT_CREATED, starling_rootCreatedHandler);

                super.initialize();

                var key:String = SCREEN_ANIMATION;

                currentScreen = screens[key] = createScreen(key);
                addElement(currentScreen);
            }
        ]]>
    </fx:Script>


</s:WindowedApplication>
